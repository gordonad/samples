apply plugin: "eclipse"
apply plugin: "idea"
apply plugin: "war"
apply plugin: "java"
apply plugin: "maven"
apply plugin: "jetty"

defaultTasks 'clean', 'build', 'install'

sourceCompatibility = 1.6
targetCompatibility = 1.6

dependencies {
    compile("org.springframework:spring-context:${springVersion}") {
        exclude module: "commons-logging"
    }
    compile("org.springframework:spring-core:${springVersion}") {
        exclude module: "commons-logging"
    }
    compile("org.springframework:spring-context-support:${springVersion}") {
        exclude module: "quartz"
    }
    compile "org.springframework:spring-webmvc:${springVersion}"
    compile "org.springframework:spring-tx:${springVersion}"
    compile "org.springframework:spring-jdbc:${springVersion}"
    compile "org.slf4j:slf4j-api:${slf4jVersion}"
    compile "org.slf4j:jcl-over-slf4j:${slf4jVersion}"
    compile "org.slf4j:jul-to-slf4j:${slf4jVersion}"
    compile "ch.qos.logback:logback-classic:${logbackVersion}"
    compile "org.apache.commons:commons-lang3:${commonsLangVersion}"
    compile "javax.inject:javax.inject:1"
    compile "com.h2database:h2:${h2Version}"
    compile "javax.servlet:jstl:${jstlVersion}"
    providedCompile("javax.servlet:javax.servlet-api:${servletVersion}")
    providedCompile("javax.servlet.jsp:jsp-api:${jspVersion}")
    testCompile "junit:junit:${junitVersion}"
    testCompile "org.mockito:mockito-all:${mockitoVersion}"
    testCompile "org.springframework:spring-test:${springVersion}"
    testCompile "org.hamcrest:hamcrest-library:${hamcrestVersion}"
}

repositories {
    maven {
        // Look for POMs and artifacts, such as JARs, here
        url "http://repo1.maven.org/maven2"
        // Look for artifacts here if not found at the above location
        artifactUrls "http://repo.springsource.org/libs-release-remote"
        artifactUrls "http://repo.springsource.org/libs-snapshot-remote"
        artifactUrls "http://repo.springsource.org/libs-milestone-remote"
        artifactUrls "http://repo.springsource.org/libs-milestone-remote"
        artifactUrls "http://repo.gradle.org/gradle/libs-releases-local"
        artifactUrls "http://repo.gradle.org/gradle/libs-releases"
        artifactUrls "http://repo.gradle.org/gradle/plugins-releases"
    }
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = "sources"
    from sourceSets.main.allJava.srcDirs
    include "**/*.java", "**/*.aj"
}

install {
    repositories.mavenInstaller {
        pom.groupId = 'org.habuma.guestbook'
        pom.artifactId = 'guestbook'
        pom.version = '1.0.0'
        pom.packaging = 'war'
    }
}

task mappings << {
    println conf2ScopeMappings.mappings
}

task wrapper(type: Wrapper) {
    gradleVersion = "1.3"
}
